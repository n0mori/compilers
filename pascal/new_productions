S -> PROGRAMA $

PROGRAMA -> program identificador ; BLOCO .

BLOCO -> var identificador BLOCOID : TIPO ; BLOCOVAR BLOCO
BLOCOVAR -> identificador BLOCOID : TIPO ; BLOCOVAR
BLOCOVAR -> LAMBDA
BLOCOID -> , identificador BLOCOID
BLOCOID -> LAMBDA
BLOCO -> procedure identificador PARAMETROS ; BLOCO ; BLOCO
BLOCO -> function identificador PARAMETROS : identificador ; BLOCO ; BLOCO
BLOCO -> begin COMANDO BLOCOMANDO end
BLOCOMANDO -> ; COMANDO BLOCOMANDO
BLOCOMANDO -> LAMBDA

COMANDO -> VARIAVEL := EXPR
COMANDO -> identificador COMPAR
COMPAR -> ( EXPR COMPAREXPR )
COMPAR -> LAMBDA
COMPAREXPR -> , EXPR COMPAREXPR
COMPAREXPR -> LAMBDA
COMANDO -> begin COMANDO COMLOOP end
COMLOOP -> ; COMANDO COMLOOP
COMLOOP -> LAMBDA
COMANDO -> if EXPR then begin COMANDO COMLOOP end COMELSE
COMELSE -> else begin COMANDO COMLOOP end
COMELSE -> LAMBDA
COMANDO -> while EXPR do COMANDO
COMANDO -> LAMBDA

TIPO -> identificador
TIPO -> CONSTANTE .. CONSTANTE
TIPO -> array [ TIPO TIPOARRAY ] of TIPO
TIPOARRAY -> , TIPO TIPOARRAY
TIPOARRAY -> LAMBDA

PARAMETROS -> ( PARAMVAR identificador PARAMID : identificador PARAMSCOLON )
PARAMSCOLON -> , PARAMVAR identificador PARAMID : identificador PARAMSCOLON
PARAMSCOLON -> LAMBDA
PARAMVAR -> var
PARAMVAR -> LAMBDA
PARAMETROS -> LAMBDA

VARIAVEL -> identificador VARSQUARE
VARSQUARE -> [ EXPR VAREXPR ]
VARSQUARE -> LAMBDA
VAREXPR -> , EXPR VAREXPR
VAREXPR -> LAMBDA

EXPR-> SIMPLEXPR CMPEXPR
CMPEXPR -> CMP SIMPLEXPR
CMPEXPR -> LAMBDA
CMP -> =
CMP -> <>
CMP -> <
CMP -> <=
CMP -> >=
CMP -> >

SIMPLEXPR -> SIGN TERMO OPSIMP
SIGN -> +
SIGN -> -
SIGN -> LAMBDA
OPSIMP -> SIMPSIGN TERMO OPSIMP
OPSIMP -> LAMBDA
SIMPSIGN -> +
SIMPSIGN -> -
SIMPSIGN -> or

TERMO -> FATOR OPFATOR
OPFATOR -> OPSIGN FATOR OPFATOR
OPFATOR -> LAMBDA
OPSIGN -> *
OPSIGN -> div
OPSIGN -> and

FATOR -> VARIAVEL
FATOR -> numero
FATOR -> identificador ( EXPR FATOREXPR )
FATOREXPR -> , EXPR FATOREXPR
FATOREXPR -> LAMBDA
FATOR -> ( EXPR )
FATOR -> not FATOR

CONSTANTE -> CONSTSIGN numero
CONSTANTE -> identificador
CONSTSIGN -> +
CONSTSIGN -> -
CONSTSIGN -> LAMBDA